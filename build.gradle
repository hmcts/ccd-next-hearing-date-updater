plugins {
  id 'application'
  id 'idea'
  id 'pmd'
  id 'jacoco'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'com.github.hmcts.rse-cft-lib' version '0.19.1847'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.25'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'info.solidsoft.pitest' version '1.15.0' apply false
  id 'org.owasp.dependencycheck' version '11.1.1'
  id 'org.sonarqube' version '6.0.1.5171'
  id 'org.springframework.boot' version '3.4.1'
  id 'uk.gov.hmcts.java' version '0.12.63'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  cftlibRuntimeClasspath.extendsFrom(runtimeClasspath)
}

idea {
  module {
    // config to allow Intellij to mark test source and resource files correctly to help linting tools
    testSourceDirs += project.sourceSets.cftlib.java.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testResourceDirs += project.sourceSets.cftlib.resources.srcDirs
    testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}


tasks.withType(Checkstyle) {
  exclude '**/package-info.java'
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  generateCucumberReports.enabled = false
  failFast = true
}

task functional(type: JavaExec) {
  dependsOn 'bootJar'

  doFirst {
    // clear away any previous BEFTA report output to prevent it being re-processed after a pre-cucumber error
    cleanBeftaReports()

    loadBeftaEnvVariables(it)
  }

  description = "Runs functional tests"
  group = "Verification"

  generateCucumberReports.enabled = false
  mainClass = 'uk.gov.hmcts.reform.next.hearing.date.updater.befta.ServiceBeftaMain'
  classpath += sourceSets.functionalTest.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
  args = [
    '--threads', '1', // Setting this to single thread as the application is not designed to have the FTs run in parallel
    '--plugin', "json:${rootDir}/target/cucumber.json",
    '--plugin', "junit:${buildDir}/test-results/functional/cucumber.xml",
    '--tags', 'not @Ignore',
    '--glue', 'uk.gov.hmcts.befta.player', 'src/functionalTest/resources/features'
  ]
  // '--add-opens=...' added to suppress 'WARNING: An illegal reflective access operation has occurred' in uk.gov.hmcts.befta.util.CucumberStepAnnotationUtils
  jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]

  finalizedBy {
    generateCucumberReports.enabled = true
    generateCucumberReports {
      doLast{
        delete "${buildDir}/reports/tests/functional/"
        new File("${buildDir}/reports/tests/functional").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${buildDir}/reports/tests/functional"))
        logger.quiet("Functional test report moved to ---> file://${buildDir}/reports/tests/functional/overview-features.html")
      }
    }
  }

  outputs.upToDateWhen { false }
}

task localDataSetup(type: JavaExec) {
  generateCucumberReports.enabled = false

  doFirst {
    // NB: must delete befta local run details to force re-import of def file
    delete "befta_recent_executions_info_LOCAL.json"

    loadBeftaEnvVariables(it)
  }

  description = "Loads BEFTA test definitions"
  group = "Verification"

  mainClass = "uk.gov.hmcts.reform.next.hearing.date.updater.befta.DataSetupApp"
  classpath += sourceSets.functionalTest.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
  args = [
    'LOCAL'
  ]
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

cucumberReports {
  outputDir = file("${rootDir}/target/cucumber")
  reports = files("${rootDir}/target/cucumber.json")
}

void cleanBeftaReports() {
  logger.quiet("Delete all previous BEFTA report files.")
  delete "${buildDir}/test-results/functional/"
  delete "${buildDir}/reports/tests/functional/"
  delete "${rootDir}/target/cucumber"
  delete "${rootDir}/target/cucumber.json"
}

void loadBeftaEnvVariables(Task execTask) {
    boolean ciBuild = System.getenv().get("CI_BUILD") ?: false
    if (!ciBuild) {
      loadEnvFile(execTask, '.env.befta.local.env')
      // Override with alternative test stub environment variables (i.e. if using bootWithCcd: need alternative test stub URL)
      loadEnvFile(execTask, '.env.test.stub.service.env')
    }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  toolVersion = '10.3.1'
  maxWarnings = 0
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.41.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ccd-next-hearing-date-updater"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ccd-next-hearing-date-updater"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.coverage.exclusions", "**/ccd/ApplicationBootstrap.java"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"

  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

apply from: './gradle/suppress.gradle'
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'

}

repositories {
  mavenLocal()
  mavenCentral()
  maven { 
    name = "AzureArtifacts"
    url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
  }
}

apply plugin: 'info.solidsoft.pitest'

pitest {
  junit5PluginVersion.set('1.2.1')
  targetClasses = ['uk.gov.hmcts.reform.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.ccd.madeup.*'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 50
  timeoutConstInMillis = 30000
  mutators.set([
    'CONDITIONALS_BOUNDARY',
    'INCREMENTS',
    'INVERT_NEGS',
    'MATH',
    'NEGATE_CONDITIONALS',
    'VOID_METHOD_CALLS',
    'EMPTY_RETURNS',
    'FALSE_RETURNS',
    'NULL_RETURNS',
    'PRIMITIVE_RETURNS',
    'TRUE_RETURNS',
    'REMOVE_CONDITIONALS'
  ])
}

project.tasks['pitest'].group = "Verification"

ext {
  set('springCloudVersion', '2024.0.0')
  set('spring-framework.version', '6.2.1')
  set('spring-security.version', '6.4.2')
  set('log4j2.version', '2.17.1')
  set('jackson.version', '2.18.0')
  set('snakeyaml.version', '2.2')
  junit = '5.10.5'
  junitPlatform = '1.10.5'
  reformLogging = '6.0.1'
  lombokVersion = '1.18.34'
  // test dependencies
  cucumberVersion = '7.20.1'
  elasticsearchVersion = '7.17.8'
  testcontainersVersion = '1.20.3'
  gradlePitestVersion = '1.15.0'
  pitestVersion = '1.17.1'
  sonarPitestVersion = '0.5'
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${junit}",
    "org.junit.jupiter:junit-jupiter-engine:${junit}",
    "org.junit.jupiter:junit-jupiter-params:${junit}",
    "org.junit.platform:junit-platform-commons:${junitPlatform}",
    "org.junit.platform:junit-platform-engine:${junitPlatform}",
    "org.apiguardian:apiguardian-api:1.1.2"
  ]
]

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

dependencies {

  // start::CVE Vulnerability dependency overrides                                                    // MAIN PARENT DEPENDEDNCY

  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.16'                  // logging-appinsights
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.16'                     // logging-appinsights
  implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'                           // idam-java-client
  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'              // idam-java-client
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.5'                 // idam-java-client
  implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.23'                        // befta-fw
  implementation group: 'org.apache.groovy', name: 'groovy-xml', version: '4.0.23'                    // befta-fw
  implementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.23'                   // befta-fw 

  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.0'       // ccd-test-definitions

  // end::CVE Vulnerability dependency overrides                                                      // MAIN PARENT DEPENDEDNCY
  
  
  // Spring
  implementation group: 'org.springframework', name:'spring-webmvc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  implementation group: 'org.springframework.security', name: 'spring-security-config'
  implementation group: 'org.springframework.security', name: 'spring-security-core'
  implementation group: 'org.springframework.security', name: 'spring-security-crypto'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-web'
  implementation group: 'org.springframework.security', name: 'spring-security-rsa', version: '1.1.5'

  // HMCTS
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.1'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLogging
  implementation(group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLogging) {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'
  }

  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: '2.6.4'
  implementation group: 'commons-validator', name: 'commons-validator', version: '1.9.0'
  implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'
  implementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.15.0'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
  implementation (group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: '0.5') {
    exclude module: "guava"
  }
  implementation group: 'org.json', name: 'json', version: '20240303'
  implementation group: 'org.pitest', name: 'pitest', version: '1.16.3'
  implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion


  // Unit test dependencies
  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.4'

  testImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.25.4'


  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  // CFTLib dependencies
  cftlibImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.2'
  cftlibImplementation group: 'org.testcontainers', name: 'testcontainers', version: testcontainersVersion
  cftlibAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion


  // Integration test dependencies
  integrationTestImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
  integrationTestImplementation group: 'org.wiremock.integrations', name:'wiremock-spring-boot', version: '3.6.0'
  integrationTestImplementation group: 'org.elasticsearch', name: 'elasticsearch', version: elasticsearchVersion
  integrationTestImplementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-client', version: elasticsearchVersion
  integrationTestImplementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: elasticsearchVersion
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainersVersion
  integrationTestImplementation group: 'org.testcontainers', name: 'elasticsearch', version: testcontainersVersion
  integrationTestImplementation group: 'com.pivovarit', name: 'throwing-function', version: '1.6.1'
  integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion


  // Functional test dependencies
  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation sourceSets.integrationTest.runtimeClasspath
  functionalTestImplementation group: 'io.cucumber', name: 'cucumber-junit', version: cucumberVersion
  functionalTestImplementation group: 'io.cucumber', name: 'cucumber-java', version: cucumberVersion
  functionalTestImplementation group: 'io.vavr', name: 'vavr', version: '0.10.5'

}

application {
  mainClass = 'uk.gov.hmcts.reform.next.hearing.date.updater.ApplicationBootstrap'
}

bootJar {
  archiveFileName = "ccd-next-hearing-date-updater.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

bootWithCCD {
  standardInput = System.in

  doFirst {
    // NB: must delete befta local run details to force re-import of def file with updated test stub URL
    delete "befta_recent_executions_info_LOCAL.json"

    // Set this flag to 'true', if you want the CFTLib containers to be recreates each time
    environment "RSE_LIB_CLEAN_BOOT", false
  }

  authMode = uk.gov.hmcts.rse.AuthMode.Local
  environment("DATA_STORE_S2S_AUTHORISED_SERVICES", "ccd_gw,ccd_gateway,ccd_data,aac_manage_case_assignment,xui_webapp,ccd_case_document_am_api,am_role_assignment_service,ccd_next_hearing_date_updater")
  environment("RSE_LIB_S2S_PORT", 4502)
  environment("XUI_JURISDICTIONS", "BEFTA_MASTER,AUTOTEST1,BEFTA_JURISDICTION_1,BEFTA_JURISDICTION_2,BEFTA_JURISDICTION_3")
}

void loadEnvFile(Task execTask, String envFilePath) {
  if (file(envFilePath).exists()) {
    logger.quiet("Loading environment variables from '${envFilePath}'")

    file(envFilePath).readLines().each() {
      if (it.startsWith('# ')) {
        if (it.startsWith('# WARNING')) {
          // copy warning to console
          logger.quiet(it)
        }
      } else if (!it.isEmpty()) {
        def (key, value) = it.tokenize('=')
        execTask.environment(key, value)
      }
    }
  } else {
    logger.quiet("Skipping load of environment variables from '${envFilePath}' as file not found.")
  }
}
